{
 "cells": [
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "###### STH co-infection model ######\n",
    "\n",
    "using Distributions #Package contaning negative binomial distribution\n",
    "\n",
    "\n",
    "###### Making NegativeBinomial faster ######\n",
    "#Courtesy of my partner Mike who does programming all the time\n",
    "\n",
    "#struct NegativeBinomial{T}"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "outputs": [],
   "cell_type": "markdown",
   "source": [
    " p0::T\n",
    " cdf::Vector{T}"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "#end\n",
    "\n",
    "#function NegativeBinomial{T}(r::Real,p::Real) where T"
   ],
   "metadata": {},
   "execution_count": 2
  },
  {
   "outputs": [],
   "cell_type": "markdown",
   "source": [
    " ps = cdf.(Distributions.NegativeBinomial(r,p), 0:40)\n",
    " NegativeBinomial{T}(ps[1], ps)"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      " 14.395796 seconds (226.14 M allocations: 15.784 GiB, 9.16% gc time)\n"
     ]
    },
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "1"
     },
     "metadata": {},
     "execution_count": 3
    }
   ],
   "cell_type": "code",
   "source": [
    "#end\n",
    "\n",
    "###### global parameters ######\n",
    "\n",
    "#It is faster to define them as constants but we can't overwrite them\n",
    "const ts = 1/73 #time step in years; 5 days\n",
    "const halflife = 8.1 #halflife of immunity in hosts in days\n",
    "const av_age = 18.2 #Average age in years in population - used very roughly\n",
    "const pc_dr = 8/1000 * ts #per capita death rate\n",
    "const stool_samp = 0.054 #Stool sample used in measuring egg deposition\n",
    "\n",
    "\n",
    "###### Worm species specific parameters ######\n",
    "\n",
    "#names\n",
    "sp = [:Na, :Al, :Tt]\n",
    "\n",
    "#I am defining a data structure with the elements b to init_pars, which will all be of the same type (class)\n",
    "#We will be able to assign a set of values to this structure (see below), and will be able to access\n",
    "#elements in the structure using '.', e.g N_a.b\n",
    "#T means type, e.g. Int, Float64.\n",
    "#The struct is mutable so that parameters can vary if necessary\n",
    "\n",
    "mutable struct Par{T}\n",
    "  b                    ::T   # 1.rate of exposure per day - has to be v v low\n",
    "  Imme_activation      ::T   # 2.to fit - activation of immunity per worm\n",
    "  Immf_activation      ::T   # 3.as above but for anti fecundity immunity\n",
    "  WfN                  ::T   # 4.max eggs per day per female worm\n",
    "  mu_le                ::T   # 5.death rate of early larvae per day\n",
    "  mu_ll                ::T   # 6.death rate of late larvae per day\n",
    "  mu_adults            ::T   # 7.death rate of adult worms per day\n",
    "  M_le                 ::T   # 8.rate of maturation of early larvae per day\n",
    "  M_ll                 ::T   # 9.rate of maturation of late larvae per day\n",
    "  pool_egg_loss        ::T   # 10.rate of loss of eggs from field per day\n",
    "  pool_egg_maturation  ::T   # 11.rate of maturation of eggs in field oer day\n",
    "  pool_infectives_loss ::T   # 12.rate of loss of infective larvae from field per day\n",
    "  dens_effect          ::T   # 13.density dependent effect on per worm fecundity\n",
    "  k                    ::T   # 14.k of negative binomial distribution (mean, k)\n",
    "  weightings           ::T   # 15.relative weight of worms\n",
    "  est_modulation       ::T   # 16.modulation of antiestablishment immunity\n",
    "  fec_modulation       ::T   # 17.modulation of antifecundity immunity\n",
    "  init_mean            ::T   # 18.mean of initial distribution of worms in pop.\n",
    "end\n",
    "\n",
    "#Inputting parameters for each species\n",
    "N_a = Par{Float64}(\n",
    "    1.9e-10 * 365 * ts,\n",
    "    0.001, 0.001, #1,2,3\n",
    "    20000 * 365 * ts, 0.067 * 365 * ts, 0.00182 * 365 * ts, #4,5,6\n",
    "    0.00182 * 365 * ts, 0.07 * 365 * ts, 0.0467 * 365 * ts, #7,8,9\n",
    "    0.011 * 365 * ts, 0.11 * 365 * ts, 0.15 * 365 * ts, #10,11,12\n",
    "    0.019, 0.23, 0.037, 0, 0, 2) #13,14,15,16,17,18\n",
    "\n",
    "\n",
    "A_l = Par{Float64}(\n",
    "    1.65e-11 * 365 * ts,\n",
    "    0.001, 0.001, #1,2,3\n",
    "    200000 * 365 * ts, 0.067 * 365 * ts, 0.00183 * 365 * ts, #4,5,6\n",
    "    0.00183 * 365 * ts, 0.10 * 365 * ts, 0.0714 * 365 * ts, #7,8,9\n",
    "    0.0085 * 365 * ts, 0.0286 * 365 * ts, 0.03 * 365 * ts, #10,11,12\n",
    "    0.00425, 0.57, 1, 0, 0, 0.5) #13,14,15,16,17,18\n",
    "\n",
    "T_t = Par{Float64}(\n",
    "    3.8e-12 * 365 * ts,\n",
    "    0.001, 0.001, #1,2,3\n",
    "    20000 * 365 * ts, 1, 0.00182 * 365 * ts, #4,5,6\n",
    "    0.00182 * 365 * ts, 0.4 * 365 * ts, 0.0133 * 365 * ts, #7,8,9\n",
    "    0.00192 * 365 * ts, 0.0286 * 365 * ts, 0.05 * 365 * ts, #10,11,12\n",
    "    0.001, 0.35, 0.0148, 0, 0, 0.5) #13,14,15,16,17,18\n",
    "\n",
    "#We keep these three Pars structs in an array, accessing as SpPars[1] etc\n",
    "SpPars = [N_a, A_l, T_t]\n",
    "\n",
    "##### Host attributes data structures #####\n",
    "\n",
    "#Each host has infections attributes per species in a data structure:\n",
    "\n",
    "struct Infection{T}\n",
    "    Imme    ::T #Anti-establishment immunity strength\n",
    "    Immf    ::T #Anti-fecundity immunity strength\n",
    "    PEL     ::T #Pre establishment larvae\n",
    "    EL      ::T #Established larvae\n",
    "    AW      ::T #Adult worms\n",
    "    EOut    ::T #Egg output\n",
    "end\n",
    "\n",
    "#We define a method so that we can easily make an empty structure.\n",
    "Infection{T}() where T = Infection{T}(0,0,0,0,0,0)\n",
    "\n",
    "##### Pool attributes data structre #####\n",
    "\n",
    "struct Soil{T}\n",
    "    PIS ::T #Pre-infective stages\n",
    "    IS  ::T #Infective stages\n",
    "end\n",
    "\n",
    "Soil{T}() where T = Soil{T}(0,0)\n",
    "\n",
    "##### model functions ######\n",
    "\n",
    "#arguements are: individual, worm burden, infective stages in pool, halflife, parameters\n",
    "function update_Infection(i, WB, IS, halflife, p)\n",
    "    #New infections\n",
    "    mean = p.b * IS\n",
    "    if mean > 0\n",
    "        #exposure = float(rand(NegativeBinomial{Float64}(mean + float(1e-6), p.k)))\n",
    "        exposure = float(rand(NegativeBinomial(mean, p.k)))\n",
    "    else\n",
    "        exposure = 0.0\n",
    "    end\n",
    "    newPEL = ((1-p.mu_le) * i.PEL) + exposure\n",
    "    PEL = newPEL * (1-p.M_le)\n",
    "\n",
    "    #Anti establishment immunity. We assume this is happening sequentially, using WB from the\n",
    "    #previous time step - we are not taking the AW into account yet as we have not yet calculated them\n",
    "    activation = PEL * p.Imme_activation\n",
    "    modulation = exp(-(p.est_modulation * WB))\n",
    "    Imme = ((0.5^(1/halflife) * i.Imme) + activation) * modulation\n",
    "\n",
    "    #New established larvae\n",
    "    newEL = ((1-p.mu_ll) * i.EL) + (p.M_le * newPEL * exp(-Imme))\n",
    "    EL = newEL * (1-p.M_ll)\n",
    "\n",
    "    #Anti fecundity immunity\n",
    "    activation = EL * p.Immf_activation\n",
    "    modulation = exp(-(p.fec_modulation * WB))\n",
    "    Immf = ((0.5^(1/halflife) * i.Immf) + activation) * modulation\n",
    "\n",
    "    #New adults\n",
    "    AW = ((1-p.mu_adults) * i.AW) + (p.M_le * newEL)\n",
    "\n",
    "    #New eggs - we are using the current time step's AW but the previous timestep's\n",
    "    #WB in Imme and Immf as we assume they were altered only once per timestep\n",
    "    modulation = exp(-(Immf + (p.dens_effect * AW/2)))\n",
    "    EOut = AW/2 * p.WfN * modulation\n",
    "\n",
    "    Infection{Float64}(Imme, Immf, PEL, EL, AW, EOut)\n",
    "end\n",
    "\n",
    "function update_WBs(pop, pars, n_hosts)\n",
    "  [sum([x.AW for x in pop[i,:]] .* [p.weightings for p in pars]) for i in 1:n_hosts]\n",
    "end\n",
    "\n",
    "#Arguments: soil, population of infections, parameters\n",
    "function update_pool(S, pop, p)\n",
    "    Eggs = sum([x.EOut for x in pop])\n",
    "    PIS = ((1 - (p.pool_egg_loss + p.pool_egg_maturation)) * S.PIS) + Eggs\n",
    "    IS = ((1-p.pool_infectives_loss) * S.IS) + (p.pool_egg_maturation * S.PIS)\n",
    "    Soil{Float64}(PIS, IS)\n",
    "end\n",
    "\n",
    "#Birth and death process - select all individuals over 80 years old and randomly select from the rest\n",
    "#of the population - reset ages and infections so population size remains constant\n",
    "function reset_inds_sys(population, ages, pc_dr)\n",
    "  for i = 1:length(ages)\n",
    "    if ages[i] > 80 || rand(Binomial(1, pc_dr)) == 1\n",
    "      population[i, 1:3] .= Infection{Float64}()\n",
    "      ages[i] = 0\n",
    "    end\n",
    "  end\n",
    "  return population, ages\n",
    "end\n",
    "\n",
    "function update_ages(ages, ts)\n",
    "    [a += ts for a in ages]\n",
    "end\n",
    "\n",
    "\n",
    "#Set up initial arrays etc. Can return multiple objects.\n",
    "function SystemSetUp(n_hosts, pars, av_age)\n",
    "    #Initialise ages, roughly scattered around average age, avoiding negative ages using abs\n",
    "    ages = [abs(rand(Normal(av_age, av_age))) for i in 1:n_hosts]\n",
    "\n",
    "    #Initialise pool with eggs and infective stages\n",
    "    Pool = [Soil{Float64}(100, 1000000) for sp in 1:3]\n",
    "\n",
    "    #Initialise worms with n. binom draw for adults\n",
    "    init_worms(r, p) = Infection{Float64}(0, 0, 0, 0, float(rand(NegativeBinomial(r,p))), 0)\n",
    "    pop_infections = [init_worms(p.init_mean, p.k) for i in 1:n_hosts, p in pars]\n",
    "\n",
    "    #Initialise worms burdens based on initial adult burdens\n",
    "    WBs = update_WBs(pop_infections, pars, n_hosts)\n",
    "    return ages, Pool, pop_infections, WBs\n",
    "end\n",
    "\n",
    "function run_mod(n_hosts, pars, ts, halflife, pop_infections, Pool, ages, WBs, pc_dr)\n",
    "\n",
    "    #Update ages and remove some individuals\n",
    "    ages = update_ages(ages, ts)\n",
    "    pop_infections, ages = reset_inds_sys(pop_infections, ages, pc_dr)\n",
    "\n",
    "    #Species specific calculations\n",
    "    for sp in 1:3\n",
    "        p = SpPars[sp]\n",
    "        IS = Pool[sp].IS\n",
    "\n",
    "        #Per host calculations\n",
    "        for i in 1:n_hosts\n",
    "            pop_infections[i, sp] = update_Infection(pop_infections[i, sp], WBs[i], IS, halflife, p)\n",
    "        end\n",
    "\n",
    "        #Update pool\n",
    "        Pool[sp] = update_pool(Pool[sp], pop_infections[:, sp], p)\n",
    "    end\n",
    "\n",
    "    #Update worm burdens\n",
    "    WBs = update_WBs(pop_infections, SpPars, n_hosts)\n",
    "\n",
    "    return ages, pop_infections, Pool, WBs\n",
    "end\n",
    "\n",
    "function main(n_runs, n_hosts, pars, av_age, ts, halflife, pc_dr, stool_samp)\n",
    "\n",
    "  #Set arrays up\n",
    "  ages, Pool, pop_infections, WBs = SystemSetUp(n_hosts, pars, av_age)\n",
    "\n",
    "  #For storing summary statistics\n",
    "  EC = zeros(Float64, n_runs, 3)\n",
    "  prevs = zeros(Float64, n_runs, 3)\n",
    "  rec = 0#n_runs - 6\n",
    "\n",
    "  #Loop through the runs\n",
    "  for r in 1:n_runs\n",
    "    ages, pop_infections, Pool, WBs = run_mod(n_hosts, pars, ts, halflife, pop_infections, Pool, ages, WBs, pc_dr)\n",
    "    if r > rec\n",
    "      for sp in 1:3\n",
    "        #Take mean egg output from only infected individuals\n",
    "        eggs = filter(!iszero, [x.EOut for x in pop_infections[:,sp]])\n",
    "        EC[r - rec,sp] = mean(eggs)/(365 * ts) * stool_samp\n",
    "        prevs[r - rec,sp] = count(i -> i > 0.0, x.AW for x in pop_infections[:,sp])/n_hosts\n",
    "      end\n",
    "    end\n",
    "  end\n",
    "#  EC = [log(mean(EC[:,sp])) for sp in 1:3]\n",
    "#  prevs = [mean(prevs[:,sp]) for sp in 1:3]\n",
    "  return EC, prevs, pop_infections\n",
    "end\n",
    "\n",
    "#Currently taking about 10 secs, give or take\n",
    "@time EC, prevs, pop = main(10000, 1000, SpPars, 18.2, ts, halflife, pc_dr, stool_samp)\n",
    "\n",
    "using Plots\n",
    "\n",
    "plot(1:10000, EC)\n",
    "plot(1:10000, prevs)\n",
    "\n",
    "N_a.b = 1"
   ],
   "metadata": {},
   "execution_count": 3
  },
  {
   "outputs": [],
   "cell_type": "markdown",
   "source": [
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "0.6.2"
  },
  "kernelspec": {
   "name": "julia-0.6",
   "display_name": "Julia 0.6.2",
   "language": "julia"
  }
 },
 "nbformat": 4
}
